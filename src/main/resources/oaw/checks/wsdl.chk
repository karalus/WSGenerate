import com::artofarc::wsimport;
import org::w3::_2001::xmlschema;
import javax::wsdl;

extension oaw::extensions::db;
extension oaw::extensions::wsdl;

context Model ERROR "dbSchema must be set (-B dbSchema=<dbSchema>)" : getDBSchema() != null;

context Model ERROR "Filenames must be unique. Check mapping.properties!" :
	serviceNamespaces.XSD2PackageName().toSet().size == serviceNamespaces.size;

context Definition ERROR targetNamespace + ": Operation has no input element " + getSOAPBindings().collect(b|b.getOperations().selectFirst(o|getInputElementQName(b, o.name) == null).name) :
	getSOAPBindings().forAll(b|b.getOperations().forAll(o|getInputElementQName(b, o.name) != null));

context ServiceNamespace WARNING "More than one Schema contributed to this targetNamespace: " + URI :
	schemas.size <= 1;

context ServiceNamespace WARNING "More than one WSDL contributed to this targetNamespace: " + URI :
	wsdls.size <= 1;

context ServiceNamespace WARNING "elementFormDefault should be the same throughout all schemas within a targetNamespace: " + URI :
	let l = schemas.toList() : l.withoutFirst().forAll(s|s.elementFormDefault == l.first().elementFormDefault);
